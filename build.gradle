plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = '1.2'
group = 'de.bottlecaps'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.basex:basex:10.7'
}

testing {	
    suites {
        test {
            useJUnitJupiter('5.10.1')
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs += ['-Xss4m']
    options.encoding = "UTF-8"
}

jar {
    manifest {
        attributes(
            'Main-Class': 'de.bottlecaps.markup.Blitz'
        )
    }
}

test {
    if (project.hasProperty('ALL_TESTS') && project.ALL_TESTS.toBoolean()) {
        maxHeapSize = '16g'
        systemProperty 'ALL_TESTS', 'true'
        jvmArgs('-Xss4m')
    }
}

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

void configureMetadata(MavenPublication mavenPublication) {
    configure (mavenPublication) {
        pom {
            name = groupId + ':' + artifactId
            description = 'Markup Blitz Parser Generator'
            url = 'https://github.com/GuntherRademacher/markup-blitz'
            licenses {
                license {
                    name = 'Apache-2.0'
                    url = 'https://opensource.org/licenses/Apache-2.0'
                }
            }
            developers {
                developer {
                    name = 'Gunther Rademacher'
                    email = 'grd@gmx.net'
                    organization = 'Gunther Rademacher'
                    organizationUrl = 'https://www.bottlecaps.de/'
                }
            }
            scm {
                connection = 'git@github.com:GuntherRademacher/markup-blitz.git'
                developerConnection = 'git@github.com:GuntherRademacher/markup-blitz.git'
                url = 'https://github.com/GuntherRademacher/markup-blitz'
            }
        }
    }
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }

    publications {
        jar(MavenPublication) {
            artifactId = 'markup-blitz'
            from components.java
            configureMetadata delegate
        }
    }
}

signing {
    sign publishing.publications.jar
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}