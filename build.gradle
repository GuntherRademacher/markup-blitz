plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = '1.10'
group = 'de.bottlecaps'

ext {
    targetJavaVersion = 17
}

println "Building project '${project.name}' targeting Java ${targetJavaVersion} bytecode"

def buildTime = new Date()
def generatedSrc = "$buildDir/generated-src/main"

sourceSets {
    main {
        java {
            srcDirs += ["$generatedSrc/java"]
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.basex:basex:12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

task generateSrc {
    doFirst {
        def blitzVersion = new File("$generatedSrc/java/de/bottlecaps/markup/blitz", 'Version.java')
        blitzVersion.parentFile.exists() || blitzVersion.parentFile.mkdirs()
        blitzVersion.withWriter {
            it << $/package de.bottlecaps.markup.blitz;

public class Version
{
  public static final String VALUE = "$version";
  public static final String DATE = "${new java.text.SimpleDateFormat("MMM dd, yyyy", Locale.US).format(buildTime)}";
}
/$
        }
    }
}

compileJava {
    dependsOn generateSrc
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(targetJavaVersion)
    options.fork = true
    options.forkOptions.jvmArgs += ['-Xss4m', '-Duser.language=en']
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
            'Main-Class': 'de.bottlecaps.markup.Blitz',
            'Implementation-Version': project.version,
            'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        )
    }
    archiveFileName = "${project.name}.jar"
}

test {
    systemProperty 'jdk.xml.maxElementDepth', '10000'
    if (project.hasProperty('ALL_TESTS') && project.ALL_TESTS.toBoolean()) {
        maxHeapSize = '16g'
        systemProperty 'ALL_TESTS', 'true'
        jvmArgs('-Xss4m')
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "${result.testCount} tests completed: " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.skippedTestCount} skipped, " +
                    "${result.failedTestCount} failed."
        }
    }
}

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

void configureMetadata(MavenPublication mavenPublication) {
    configure (mavenPublication) {
        pom {
            name = groupId + ':' + artifactId
            description = 'Markup Blitz Parser Generator'
            url = 'https://github.com/GuntherRademacher/markup-blitz'
            licenses {
                license {
                    name = 'Apache-2.0'
                    url = 'https://opensource.org/licenses/Apache-2.0'
                }
            }
            developers {
                developer {
                    name = 'Gunther Rademacher'
                    email = 'grd@gmx.net'
                    organization = 'Gunther Rademacher'
                    organizationUrl = 'https://www.bottlecaps.de/'
                }
            }
            scm {
                connection = 'git@github.com:GuntherRademacher/markup-blitz.git'
                developerConnection = 'git@github.com:GuntherRademacher/markup-blitz.git'
                url = 'https://github.com/GuntherRademacher/markup-blitz'
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'local'
            def releaseRepo = layout.buildDirectory.dir('repo/releases')
            def snapshotRepo = layout.buildDirectory.dir('repo/snapshots')
            url = isReleaseVersion ? releaseRepo : snapshotRepo
        }
    }

    publications {
        jar(MavenPublication) {
            artifactId = 'markup-blitz'
            from components.java
            configureMetadata delegate
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.jar
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

tasks.withType(AbstractPublishToMaven).configureEach {
    doFirst {
        println "Preparing to publish... Checking JDK environment."

        def requiredLauncher = javaToolchains.launcherFor {
            languageVersion.set(JavaLanguageVersion.of(targetJavaVersion as int))
        }.get()

        if (JavaVersion.current().ordinal() > JavaVersion.toVersion(targetJavaVersion).ordinal()) {
            throw new GradleException("Cannot publish with JDK ${JavaVersion.current()} â€” must use Java ${targetJavaVersion}")
        }

        println "JDK version ${JavaVersion.current()} is valid for publishing"
    }
}

tasks.register('createBundle', Zip) {
    dependsOn 'publishAllPublicationsToLocalRepository'
    from publishing.repositories.local.url
    archiveFileName = "${project.name}-${project.version}-bundle.zip"
    destinationDirectory = layout.buildDirectory.dir('bundles')
}

publish.finalizedBy createBundle
